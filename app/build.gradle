import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

apply plugin: 'com.android.application'

ext.versionMajor = 2
ext.versionMinor = 1
ext.versionPatch = 0
ext.minimumSdkVersion = 21
ext.isSnapshot = true

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    defaultConfig {
        applicationId "com.hardcodecoder.pulsemusic"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 29
        versionCode generateVersionCode()
        versionName generateVersionName()
        vectorDrawables.useSupportLibrary = true
        resConfig 'en'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField "String", "BUILD_DATE", getBuildTime()
        }
        release {
            buildConfigField "String", "BUILD_DATE", getBuildTime()
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}


private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}


private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.isSnapshot) {
        versionName += "-Debug"
    }

    else {
        versionName += "-Release"
    }
    return versionName
}


private static String getBuildTime() {
    DateTimeFormatter dtf = DateTimeFormatter.ofPattern("EEEE d MMMM yyyy HH:mm:ss")
    LocalDateTime now = LocalDateTime.now()
    String dateTime = dtf.format(now)
    return "\"${dateTime}\""
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    implementation 'androidx.media:media:1.1.0'
    implementation 'com.google.android.material:material:1.2.0'

    annotationProcessor 'androidx.annotation:annotation:1.1.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
